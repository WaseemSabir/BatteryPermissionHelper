apply plugin: 'maven-publish'
apply plugin: 'signing'


task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION


afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll setup later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
//                artifacts javaDocJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = "Battery Permission Helper Android"
                    url = 'https://github.com/WaseemSabir/BatteryPermissionHelper/tree/master/'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/WaseemSabir/BatteryPermissionHelper/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'waseemsabir'
                            name = 'Waseem Sabir'
                            email = 'waseemsabir99@gmail.com'
                        }
                    }

                    // Version control info
                    scm {
                        connection = 'scm:git:ssh://github.com:WaseemSabir/BatteryPermissionHelper.git'
                        developerConnection = 'scm:git:ssh://github.com:WaseemSabir/BatteryPermissionHelper.git'
                        url = 'https://github.com/WaseemSabir/BatteryPermissionHelper/tree/master/'
                    }
                }

            }
        }
    }
}


signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"]
    )
    sign publishing.publications
}
